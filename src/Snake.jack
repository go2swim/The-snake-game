class Snake {
  function Array init(int length) {
    var Array snake;
    var int i;
    let snake = Array.new(length);   //создаём массив длиной length, который будет хранить блоки 
    let i = 0;
    while (i < length) {
      let snake[i] = Block.new(i, 0);   //заполняем массив блоками с координатами ({0<..<len(snake)}, 0)
      do Block.drawFullBlock(snake[i], true); 
      // do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    return snake;
  }
 
  function Block getBlock(Array snake, int indx) {  //возвращает один из блоков по индексу из змейки
    return snake[indx];
  }
 
  function void moveSnake(Array snake, int length, int dir) {  //dir == 1 вправо, dir == 2 влево, dir == 3 вниз, dir == 4 вверх
    var int i, tmplength;
    let i = 0;
    let tmplength = length - 1;
    do Block.drawFullBlock(snake[i], false);    //делаем белым первый блок змейки
    do Block.dispose(snake[i]);     //удаляем обесцвеченый ранее блок
    while (i < tmplength) {
      let snake[i] = snake[i + 1];  //переприсваиваем все средние блоки змейки не включая голову и хвост
      // do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    let snake[length - 1] = Block.moveHeadBlock(snake[length - 1], dir);    //отрисовывает голову змейки в соответствии с направлением
    // do Block.printBlockCoord(snake[l - 1]);
    return;
  }

  function Array lengthenSnake(Array snake, int length, Block target) {
    var int newLength, i;
    var Array newSnake;
    do Block.drawFullBlock(target, true); //рисуем новый квадрат
    let newLength = length + 1;
    let newSnake = Array.new(newLength);  //пересоздаём змейку с новой длиной
    let i = 0;
    while (i < length) {
      let newSnake[i] = snake[i];   //переприсваеваем блоки из старой змейки в новую
      // do Block.printBlockCoord(newSnake[i]);
      let i = i + 1;
    }
    do snake.dispose();   //удаляем старую змейку
    let newSnake[i] = target;
    return newSnake;
  } 

  function void dispose(Array snake, int length) {
    var int i;
    let i = 0;
    while (i < length) {
      do Block.dispose(Snake.getBlock(snake, i));   //убираем все блоки в змейке
      let i = i + 1;
    }
    do snake.dispose();   //и убираем элементы массива 
    return;
  }
  
  // debug
  function void printSnakeCoords(Array snake, int length) { 
    var int i;
    let i = 0;
    while (i < length) {
      do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    return;
  }   
}